#version 450

#extension GL_GOOGLE_include_directive : enable

layout (local_size_x = 16, local_size_y = 16) in;

#include "scene_ubo.glsl"

layout(set = 1, binding = 0) uniform sampler2D gbuffer0Tex;
layout(set = 1, binding = 1) uniform sampler2D depthTex;
layout(set = 1, binding = 2) uniform sampler2D noiseTex;

layout(set = 1, binding = 3) uniform UniformBufferObjectSamples {
  vec4 samples[64];
} samplesUbo;

layout(set = 1, binding = 4, r32f) uniform writeonly image2D outputIm;

vec3 calcViewSpacePos(vec2 texCoord, float depthSamp)
{
  vec4 clipSpacePos = vec4(texCoord * 2.0 - vec2(1.0), depthSamp, 1.0);

  vec4 position = ubo.invProj * clipSpacePos; // Use this for view space
  //vec4 position = ubo.invViewProj * clipSpacePos; // Use this for world space

  return(position.xyz / position.w);
}

void main() {
  if (gl_GlobalInvocationID.x > ubo.screenWidth || gl_GlobalInvocationID.y > ubo.screenHeight) {
    return;
  }

  ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);
  vec2 texCoords = (vec2(pixel) + 0.5) / vec2(ubo.screenWidth, ubo.screenHeight);

  float depthSamp = texture(depthTex, texCoords).r;

  // Early exit if we're in the sky
  if (depthSamp == 1.0f) {
    imageStore(outputIm, pixel, vec4(1.0));
    return;
  }

  const vec2 noiseScale = vec2(float(ubo.screenWidth)/4.0, float(ubo.screenHeight)/4.0);

  // From learnopengl.com
  vec3 fragPos   = calcViewSpacePos(texCoords, depthSamp);

  // Early exit if we're extremely close to the camera
  if (-fragPos.z < 0.5) {
    imageStore(outputIm, pixel, vec4(1.0));
    return;
  }

  vec3 normal    = mat3(ubo.view) * (texture(gbuffer0Tex, texCoords).rgb * 2.0 - 1.0);
  vec3 randomVec = texture(noiseTex, texCoords * noiseScale).xyz;

  vec3 tangent   = normalize(randomVec - normal * dot(randomVec, normal));
  vec3 bitangent = cross(normal, tangent);
  mat3 TBN       = mat3(tangent, bitangent, normal);

  float maxDist = 10.0;

  float occlusion = 0.0;
  int kernelSize = 64;
  float radius = mix(0.01, 0.5, clamp(-fragPos.z/maxDist, 0.0, 1.0));
  float bias = 0.025;
  for(int i = 0; i < kernelSize; ++i) {
    // get sample position
    vec3 samplePos = TBN * samplesUbo.samples[i].xyz; // from tangent to view-space
    samplePos = fragPos + samplePos * radius; 
    
    vec4 offset = vec4(samplePos, 1.0);
    offset      = ubo.proj * offset;    // from view to clip-space
    offset.xyz /= offset.w;               // perspective divide
    offset.xyz  = offset.xyz * 0.5 + 0.5; // transform to range 0.0 - 1.0

    float offsetDepthSamp = texture(depthTex, offset.xy).r;
    float sampleDepth = calcViewSpacePos(offset.xy, offsetDepthSamp).z;
    
    float rangeCheck = smoothstep(0.0, 1.0, radius / abs(fragPos.z - sampleDepth));
    occlusion       += (sampleDepth >= samplePos.z + bias ? 1.0 : 0.0) * rangeCheck; 
  }

  occlusion = 1.0 - (occlusion / kernelSize);

  imageStore(outputIm, pixel, vec4(occlusion));
}