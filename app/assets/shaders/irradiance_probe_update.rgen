#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_GOOGLE_include_directive : enable

#include "bindless.glsl"
#include "bindless_tlas.glsl"
#include "scene_ubo.glsl"
#include "probe_helpers.glsl"

layout(set = 1, binding = 0, rgba16f) uniform writeonly image2D irradianceIm;
layout(set = 1, binding = 1, rgba16f) uniform writeonly image2D dirIm;

const float GOLDEN_RATIO = 1.6180339887;
const float PI = 3.14159265359;
const float EPSILON = 1e-6;

struct hitPayload
{
  vec3 irradiance;
  vec3 hitPos;
  float shadow;
};

layout(location = 0) rayPayloadEXT hitPayload payload;

layout(push_constant) uniform constants {
  mat4 randomRot;
  uint probeLayer;
} pc;

float rand(vec2 co){
  return fract(sin(dot(co, vec2(12.9898, 78.233))) * 43758.5453);
}

vec3 worldDirFromFibonacciIndex(uint idx)
{
  float theta = 2.0 * PI * float(idx) / GOLDEN_RATIO;
  float phi = acos(1.0 - 2.0 * (float(idx) + 0.5) / float(NUM_RAYS_PER_PROBE));

  vec3 outDir = vec3(
    cos(theta) * sin(phi),
    sin(theta) * sin(phi),
    cos(phi));

  return normalize(outDir);
}

void main()
{
  // Find which probe this invocation corresponds to
  ivec2 pixel = ivec2(gl_LaunchIDEXT.xy);

  int probeX = pixel.x / PROBE_IRR_DIR_PIX_SIZE;
  int probeY = int(pc.probeLayer);//pixel.y / (PROBE_IRR_DIR_PIX_SIZE * NUM_PROBES_PER_PLANE);   // THIS IS THE HEIGHT, AKA PLANE!
  int probeZ = pixel.y / PROBE_IRR_DIR_PIX_SIZE;//(pixel.y - (probeY * PROBE_IRR_DIR_PIX_SIZE * NUM_PROBES_PER_PLANE)) / PROBE_IRR_DIR_PIX_SIZE;

  ivec2 pixelWithinProbe = ivec2(pixel.x - probeX * PROBE_IRR_DIR_PIX_SIZE, pixel.y - probeZ * PROBE_IRR_DIR_PIX_SIZE);
  int rayIndex = pixelWithinProbe.y * PROBE_IRR_DIR_PIX_SIZE + pixelWithinProbe.x;

  ivec2 camProbeIdx = 
    ivec2(int(ubo.cameraPos.x / PROBE_STEP.x), int(ubo.cameraPos.z / PROBE_STEP.z));

  vec3 camOffset = vec3(
    float(camProbeIdx.x) * PROBE_STEP.x - PROBE_STEP.x * float(NUM_PROBES_PER_PLANE)/2.0,
    0.0, 
    float(camProbeIdx.y) * PROBE_STEP.z - PROBE_STEP.z * float(NUM_PROBES_PER_PLANE)/2.0);

  vec3 origin = vec3(float(probeX), float(probeY), float(probeZ)) * PROBE_STEP + camOffset;

  // Create a directional vector from the "ray index"
  vec3 worldDir = worldDirFromFibonacciIndex(rayIndex);
  worldDir = mat3(pc.randomRot) * worldDir;

  uint  rayFlags =  gl_RayFlagsOpaqueEXT; // All geometry is considered opaque
  float tMin     = 0.1;
  float tMax     = 50.0;

  payload.irradiance = vec3(0.0);

  traceRayEXT(tlas,     // acceleration structure
        rayFlags,       // rayFlags
        0xFF,           // cullMask
        0,              // sbtRecordOffset
        0,              // sbtRecordStride
        0,              // missIndex
        origin,         // ray origin
        tMin,           // ray min range
        worldDir,       // ray direction
        tMax,           // ray max range
        0);             // payload (location = 0)

  //vec3 packedDir = (worldDir.xyz + 1.0) * 0.5;
  //imageStore(dirIm, pixel, vec4(packedDir, 1.0));
  imageStore(dirIm, pixel, vec4(worldDir.xyz, 1.0));
  imageStore(irradianceIm, pixel, vec4(payload.irradiance.rgb, 1.0));

}