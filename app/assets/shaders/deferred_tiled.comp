#version 450

#extension GL_GOOGLE_include_directive : enable

layout (local_size_x = 32, local_size_y = 32) in;

#include "scene_ubo.glsl"

layout(set = 1, binding = 0) uniform sampler2D im0;
layout(set = 1, binding = 1) uniform sampler2D im1;
//layout(set = 1, binding = 2) uniform sampler2D im2;
layout(set = 1, binding = 2) uniform sampler2D depth;
layout(set = 1, binding = 3) uniform sampler2D shadowMap;
layout(set = 1, binding = 4) uniform sampler2D ssao;
layout(set = 1, binding = 5) uniform sampler2D rtShadowMask;
layout(set = 1, binding = 6, rgba16) uniform writeonly image2D outputIm;

struct Light 
{
  vec4 worldPos; // w is range
  vec4 color; // w is enabled or not
};

layout(std430, set = 0, binding = 3) readonly buffer LightBuffer {
  Light lights[];
} lightBuffer;

layout(push_constant) uniform constants {
  uint screenWidth;
  uint screenHeight;
} pushConstants;

// The index list that will need to be processed by this particular invocation
shared uint groupLocalIndexList[32 * 32]; // Worst case size is _all_ lights

// The actual size of the above array
shared uint groupLocalNumIndices;

void doLight(ivec2 pixel);

const vec3 clearColor = vec3(0.2, 0.5, 0.7);

void main()
{
  if (gl_GlobalInvocationID.x > pushConstants.screenWidth || gl_GlobalInvocationID.y > pushConstants.screenHeight) {
    return;
  }

  if (gl_LocalInvocationID.x == 0 && 
      gl_LocalInvocationID.y == 0 &&
      gl_LocalInvocationID.z == 0) {
    groupLocalNumIndices = 0;
  }

  // Early exit if we're in the sky
  ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);
  vec2 texCoords = vec2(pixel) / vec2(ubo.screenWidth - 1, ubo.screenHeight - 1);

  float depthSamp = texture(depth, texCoords).r;

  if (depthSamp == 1.0f) {
    imageStore(outputIm, pixel, vec4(clearColor, 1.0));
    return;
  }

  barrier();

  // Project a light into screen space and check if it is in our current invocation tile.
  uint lightIndex = gl_LocalInvocationID.x * gl_WorkGroupSize.y + gl_LocalInvocationID.y;
  Light light = lightBuffer.lights[lightIndex];

  if (light.color.w > 0.5) {
    vec2 center = vec2( pushConstants.screenWidth,  pushConstants.screenHeight) / float(2 * gl_WorkGroupSize.x); // Location of the middle work group
    vec2 offset = center - vec2(gl_WorkGroupID.xy);
 
    // Extract the viewing frustum planes (normals)
    // https://gamedev.stackexchange.com/questions/156743/finding-the-normals-of-the-planes-of-a-view-frustum
    vec4 column0 = vec4(-ubo.proj[0][0] * center.x, ubo.proj[0][1], offset.x, ubo.proj[0][3]); // (-2 * n' / (r - l) * 80, 0, offset.x, 0)
    vec4 column1 = vec4(ubo.proj[1][0], -ubo.proj[1][1] * center.y, offset.y, ubo.proj[1][3]); // (0, -2 * n' / (t - b) * 45, offset.y, 0)
    vec4 column3 = vec4(ubo.proj[3][0], ubo.proj[3][1], -1.0f, ubo.proj[3][3]); // (0, 0, -1, 0)
 
    vec4 frustumPlanes[4];
    // Left
    frustumPlanes[0] = column3 + column0;
    // Right
    frustumPlanes[1] = column3 - column0;
    // Top
    frustumPlanes[2] = column3 - column1;
    // Bottom
    frustumPlanes[3] = column3 + column1;
    for (int i = 0; i < 4; ++i) {
      frustumPlanes[i] /= length(frustumPlanes[i].xyz);
    }

    vec4 lightView =  ubo.view * vec4(light.worldPos.xyz, 1.0f);
    bool inFrustum = true;//distance(ubo.cameraPos.xyz, light.worldPos.xyz) < 50.0;
    inFrustum = inFrustum && lightView.z > -50.0;
    inFrustum = inFrustum && lightView.z < 1.0;
    if (inFrustum) {
      for (int j = 3; j >= 0 && inFrustum; --j) {
        float d = dot(frustumPlanes[j], lightView); // Distance of the point from the plane
        // https://gamedev.stackexchange.com/questions/79172/checking-if-a-vector-is-contained-inside-a-viewing-frustum
        inFrustum = -light.worldPos.w <= d;
      }
      if (inFrustum) {
        uint countBefore = atomicAdd(groupLocalNumIndices, 1);
        groupLocalIndexList[countBefore] = lightIndex;
      }
    }
  }

  barrier();

  doLight(pixel);
}

const float PI = 3.14159265359;

float DistributionGGX(vec3 N, vec3 H, float roughness)
{
   float a      = roughness*roughness;
   float a2     = a*a;
   float NdotH  = max(dot(N, H), 0.0);
   float NdotH2 = NdotH*NdotH;

   float num   = a2;
   float denom = (NdotH2 * (a2 - 1.0) + 1.0);
   denom = PI * denom * denom;

   return num / denom;
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
   float r = (roughness + 1.0);
   float k = (r*r) / 8.0;

   float num   = NdotV;
   float denom = NdotV * (1.0 - k) + k;

   return num / denom;
}

float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
   float NdotV = max(dot(N, V), 0.0);
   float NdotL = max(dot(N, L), 0.0);
   float ggx2  = GeometrySchlickGGX(NdotV, roughness);
   float ggx1  = GeometrySchlickGGX(NdotL, roughness);

   return ggx1 * ggx2;
}

// Based omn http://byteblacksmith.com/improvements-to-the-canonical-one-liner-glsl-rand-for-opengl-es-2-0/
float random(vec2 co)
{
   float a = 12.9898;
   float b = 78.233;
   float c = 43758.5453;
   float dt= dot(co.xy ,vec2(a,b));
   float sn= mod(dt,3.14);
   return fract(sin(sn) * c);
}

// Radical inverse based on http://holger.dammertz.org/stuff/notes_HammersleyOnHemisphere.html
vec2 hammersley2d(uint i, uint N)
{
   uint bits = (i << 16u) | (i >> 16u);
   bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
   bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
   bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
   bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
   float rdi = float(bits) * 2.3283064365386963e-10;
   return vec2(float(i) /float(N), rdi);
}

// Based on http://blog.selfshadow.com/publications/s2013-shading-course/karis/s2013_pbs_epic_slides.pdf
// https://github.com/SaschaWillems/Vulkan-glTF-PBR/blob/master/data/shaders/genbrdflut.frag
vec3 importanceSample_GGX(vec2 Xi, float roughness, vec3 normal) 
{
   // Maps a 2D point to a hemisphere with spread based on roughness
   float alpha = roughness * roughness;
   float phi = 2.0 * PI * Xi.x + random(normal.xz) * 0.1;
   float cosTheta = sqrt((1.0 - Xi.y) / (1.0 + (alpha*alpha - 1.0) * Xi.y));
   float sinTheta = sqrt(1.0 - cosTheta * cosTheta);
   vec3 H = vec3(sinTheta * cos(phi), sinTheta * sin(phi), cosTheta);

   // Tangent space
   vec3 up = abs(normal.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(1.0, 0.0, 0.0);
   vec3 tangentX = normalize(cross(up, normal));
   vec3 tangentY = normalize(cross(normal, tangentX));

   // Convert to world Space
   return normalize(tangentX * H.x + tangentY * H.y + normal * H.z);
}

vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
   return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

vec3 fresnelSchlickRoughness(float cosTheta, vec3 F0, float roughness)
{
   return F0 + (max(vec3(1.0 - roughness), F0) - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

float inShadow(vec3 worldPos) {
  vec4 shadowPos = ubo.directionalShadowMatrixProj * ubo.directionalShadowMatrixView * vec4(worldPos, 1.0);
  vec3 projCoords = shadowPos.xyz / shadowPos.w;

  if (projCoords.x > 1.0 || projCoords.x < -1.0 ||
      projCoords.z > 1.0 || projCoords.z < 0.0 ||
      projCoords.y > 1.0 || projCoords.y < -1.0) {
    return 0.0;
  }

  vec2 shadowMapCoord = projCoords.xy * 0.5 + 0.5;

  float depth = projCoords.z;
  float sampledDepth = texture(shadowMap, shadowMapCoord.xy).r;
  if (depth > sampledDepth) {
    return 1.0;
  }
  return 0.0;
}

vec3 calcWorldPos(vec2 texCoord, float depthSamp)
{
  vec4 clipSpacePos = vec4(texCoord * 2.0 - vec2(1.0), depthSamp, 1.0);

  //vec4 position = inverse_projection_matrix * clip_space_position; // Use this for view space
  vec4 position = ubo.invViewProj * clipSpacePos; // Use this for world space

  return(position.xyz / position.w);
}

void doLight(ivec2 pixel)
{
  vec2 texCoords = vec2(pixel) / vec2(ubo.screenWidth - 1, ubo.screenHeight - 1);

  vec4 im0Samp = texture(im0, texCoords);
  vec4 im1Samp = texture(im1, texCoords);
  //vec4 im2Samp = texture(im2, texCoords);
  float depthSamp = texture(depth, texCoords).r;

  if (depthSamp == 1.0f) {
    imageStore(outputIm, pixel, vec4(0.5, 0.5, 0.5, 1.0));
    return;
  }

  vec3 normal = im0Samp.rgb * 2.0 - 1.0;
  vec3 albedo = vec3(im0Samp.a, im1Samp.rg);//pow(vec3(im0Samp.a, im1Samp.rg), vec3(2.2));
  float metallic = im1Samp.b;
  float roughness = im1Samp.a;
  float depth = depthSamp;
  vec3 fragWorldPos = calcWorldPos(texCoords, depthSamp);//im2Samp.rgb;//calcWorldPos(texCoords, depthSamp);

  vec3 color = vec3(0.0f);
  vec3 lightColor = vec3(1.0);

  /* Implementation from https://learnopengl.com/PBR/Theory */
  vec3 N = normal;
  vec3 V = normalize(ubo.cameraPos.xyz - fragWorldPos);
  vec3 R = reflect(V, N);

  vec3 F0 = vec3(0.04);
  F0 = mix(F0, albedo, metallic);

  // Reflectance equation
  vec3 Lo = vec3(0.0);
  vec3 directionalLo = vec3(0.0);

  const float brightness = 5.0;

  for (int i = 0; i < 1 + groupLocalNumIndices; ++i) {
    float attenuation = 0.0f;
    vec3 L = vec3(0.0);

    if (i == 0) {
      attenuation = 1.0f; // Directional light
      L = normalize(ubo.lightDir.xyz * vec3(-1.0, 1.0, -1.0));
    }
    else {
      Light light = lightBuffer.lights[groupLocalIndexList[i-1]];
      vec3 lightWorldPos = light.worldPos.xyz;
      float d = min(distance(lightWorldPos, fragWorldPos), light.worldPos.w);
      attenuation = (1.0 - d / light.worldPos.w)*(1.0 - d/light.worldPos.w) * brightness;
      //float d = distance(lightWorldPos, fragWorldPos);
      //attenuation = clamp(brightness - d/(1.0/brightness * light.worldPos.w), 0.0, brightness);//1.0 / (d * d);
      L = normalize(lightWorldPos - fragWorldPos);
      lightColor = light.color.rgb;
    }

    vec3 H = normalize(V + L);
    vec3 radiance = lightColor * attenuation;

    // Cook-torrance brdf
    float NDF = DistributionGGX(N, H, roughness);
    float G   = GeometrySmith(N, V, L, roughness);
    vec3 F    = fresnelSchlick(max(dot(H, V), 0.0), F0);

    vec3 kS = F;
    vec3 kD = vec3(1.0) - kS;
    kD *= 1.0 - metallic;

    float NdotL = max(dot(N, L), 0.0);
    float NdotV = max(dot(N, V), 0.0);

    vec3 numerator    = NDF * G * F;
    float denominator = 4.0 * NdotV * NdotL + 0.0001;
    vec3 specular     = numerator / denominator;

    // Add to outgoing radiance Lo
    if (i == 0) {
      directionalLo += (kD * albedo / PI + specular) * radiance * NdotL;
    }
    else {
      Lo += (kD * albedo / PI + specular) * radiance * NdotL;
    }
  }

  float shadow = 1.0f;
  
  if (ubo.directionalShadowsEnabled == 1) {
    shadow = inShadow(fragWorldPos);
    shadow = clamp(1.0 - shadow, 0.3, 1.0);
  }
  else {
    shadow = 1.0 - texture(rtShadowMask, texCoords).r;
    shadow = clamp(1.0 - shadow, 0.3, 1.0);
  }

  float occlusion = 1.0f;
  if (ubo.ssaoEnabled == 1) {
    occlusion = texture(ssao, texCoords).r;  
  }

  vec3 ambient = vec3(0.3) * albedo * shadow;
  directionalLo *= shadow;
  color = (ambient + Lo + directionalLo) * occlusion;
  //color = vec3(occlusion, occlusion, occlusion);
  //color = vec3(texCoords.x, texCoords.y, 0.0);
  //color = vec3(depthSamp);
  //color = fragWorldPos;

  // DEBUG TEST
  /*vec3 red = vec3(1.0, 0.0, 0.0);
  color += red * float(groupLocalNumIndices)/10.0;*/

  // HDR tonemapping
  //color = color / (color + vec3(1.0));
  // gamma correct
  //color = pow(color, vec3(1.0/2.2)); 

  imageStore(outputIm, pixel, vec4(color, 1.0));
}